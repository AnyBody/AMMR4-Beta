/* 
  Calcuates the Bryant (euler) angles for the z-y-x rotation sequence given a 
  rotation matrix

  RotAxisAnglesZYX Angles(ROT = .SomeSeg.Axes0) = {};


  The RotAxisAnglesZYX1 version does the same, but can calulate the relative 
  rotation angles between two segments. 

  RotAxisAnglesZYX Angles(ROT = .SomeSeg.Axes0) = {
    ROT_rel = .SomeOtherSeg.Axes0;
  };


*/ 

#class_template RotAxisAnglesZYX (ROT){
 AnyFolder Calculations = {
  
      AnyVar yRot1 = asin(-.ROT[2][0]);
      AnyVar yRot2 = pi - yRot1;
      
      AnyVar zRot1 = atan2(.ROT[1][0]/cos(yRot1), .ROT[0][0]/cos(yRot1));
      AnyVar zRot2 = atan2(.ROT[1][0]/cos(yRot2), .ROT[0][0]/cos(yRot2));
      
      AnyVar xRot1 = atan2(.ROT[2][1]/cos(yRot1),.ROT[2][2]/cos(yRot1));
      AnyVar xRot2 = atan2(.ROT[2][1]/cos(yRot2),.ROT[2][2]/cos(yRot2));
      AnyVar abs1 = abs(zRot1)+abs(yRot1)+abs(zRot1);
      AnyVar abs2 = abs(zRot2)+abs(yRot2)+abs(zRot2);
    };
    
    AnyVar zRot = iffun(gtfun(Calculations.abs1,Calculations.abs2), Calculations.zRot2,Calculations.zRot1);
    AnyVar yRot = iffun(gtfun(Calculations.abs1,Calculations.abs2), Calculations.yRot2,Calculations.yRot1);
    AnyVar xRot = iffun(gtfun(Calculations.abs1,Calculations.abs2), Calculations.xRot2,Calculations.xRot1) ;
}; 

#class_template RotAxisAnglesZYX1 (ROT){
   #var AnyMat33 ROT_rel = {{1,0,0},{0,1,0},{0,0,1}};
  
    AnyFolder Calculations = {
  
      AnyMat33 Rot = .ROT_rel'*..ROT;
  
      AnyVar yRot1 = asin(-Rot[2][0]);
      AnyVar yRot2 = pi - yRot1;
      
      AnyVar zRot1 = atan2(..Rot[1][0]/cos(yRot1), ..Rot[0][0]/cos(yRot1));
      AnyVar zRot2 = atan2(..Rot[1][0]/cos(yRot2), ..Rot[0][0]/cos(yRot2));
      
      AnyVar xRot1 = atan2(..Rot[2][1]/cos(yRot1),..Rot[2][2]/cos(yRot1));
      AnyVar xRot2 = atan2(..Rot[2][1]/cos(yRot2),..Rot[2][2]/cos(yRot2));
      AnyVar abs1 = abs(zRot1)+abs(yRot1)+abs(zRot1);
      AnyVar abs2 = abs(zRot2)+abs(yRot2)+abs(zRot2);
    };
    
    AnyVar zRot = iffun(gtfun(Calculations.abs1,Calculations.abs2), Calculations.zRot2,Calculations.zRot1);
    AnyVar yRot = iffun(gtfun(Calculations.abs1,Calculations.abs2), Calculations.yRot2,Calculations.yRot1);
    AnyVar xRot = iffun(gtfun(Calculations.abs1,Calculations.abs2), Calculations.xRot2,Calculations.xRot1) ;
}; 