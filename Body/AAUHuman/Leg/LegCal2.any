//This is the calibration2 file it will calibrate specfic muscles on the leg
AnyFolder LegCal2 ={
  
  AnyFolder &Pelvis = ...HumanFolderRef.Trunk.SegmentsLumbar.PelvisSeg; //This is a reference to the folder which holds the human part of the model
  AnyFolder &LegSegSide = .SideHumanFolderRef.Leg.Seg; //This is a reference to the folder which holds the human part of the model
  
  AnyFolder &JointsCal2 =  .SideHumanFolderRef.Leg.Jnt;
  
  
  
  AnyFixedRefFrame ground = {
    AnyRefNode node = { 
       ARel=..Pelvis.Axes0;
      sRel=..Pelvis.r0;
    };
  };
  
  AnyRevoluteJoint PelvisGround={
    AnyRefFrame &Ground = .ground.node;
    AnyRefFrame &Sacrum= ....HumanFolderRef.Trunk.SegmentsLumbar.PelvisSeg; 
    Axis=z;
  };
  
  AnyKinEqSimpleDriver PelvisGroundRot = {
    AnyRevoluteJoint &Jnt = .PelvisGround;
    DriverPos = {0};
    DriverVel = {0};
    Reaction.Type = {Off}; 
  };
  
  
  
  AnyKinEqSimpleDriver JntDOFDriverRight = {
    AnyKinMeasure &Joint = ..SideHumanFolderRef.Leg.JntDOF;
    DriverPos = {1.83,0.0,0,0,0,-0.87}; //-15 degrees dorsal flexion
    DriverVel = {0.0,0.0,0,0,0,0};
    Reaction.Type = {Off,Off,Off,Off,Off,Off};
  };//JntDOFDriverRight
  

  // Calibration2 calibrates the following muscles of the lower leg:
  // Soleus, Gastrocnemius, Hamstrings, Gluteus maximus, Iliopsoas
  AnyFolder &MuscleParRightCal2 =  .SideHumanFolderRef.Leg.MusPar;
  
  AnyFolder MusRight={
    AnyViaPointMuscle &TibialisPosterior = ..SideHumanFolderRef.Leg.Mus.TibialisPosterior; //added
    AnyViaPointMuscle &Soleus =  ..SideHumanFolderRef.Leg.Mus.Soleus;
    AnyViaPointMuscle &Gastrocnemius =  ..SideHumanFolderRef.Leg.Mus.Gastrocnemius;
    AnyViaPointMuscle &GluteusMaximus2 =  ..SideHumanFolderRef.Leg.Mus.GluteusMaximus2;
    AnyViaPointMuscle &Semitendinosus =  ..SideHumanFolderRef.Leg.Mus.Semitendinosus;
//  AnyViaPointMuscle &Semimembranosus =  ..SideHumanFolderRef.Leg.Mus.Semimembranosus;
    AnyViaPointMuscle &BicepsFemorisCaputLongum =  ..SideHumanFolderRef.Leg.Mus.BicepsFemorisCaputLongum;
    AnyViaPointMuscle &Sartorius =  ..SideHumanFolderRef.Leg.Mus.Sartorius;
 
    AnyViaPointMuscle &GemmelusSuperior =  ..SideHumanFolderRef.Leg.Mus.GemmelusSuperior;
    AnyViaPointMuscle &GemmelusInferior=  ..SideHumanFolderRef.Leg.Mus.GemmelusInferior;
    AnyViaPointMuscle &Pictineus=  ..SideHumanFolderRef.Leg.Mus.Pictineus;
    AnyViaPointMuscle &ObturatoriusInternus=  ..SideHumanFolderRef.Leg.Mus.ObturatoriusInternus;
    AnyViaPointMuscle &ObturatoriusExternus=  ..SideHumanFolderRef.Leg.Mus.ObturatoriusExternus;
   
    AnyViaPointMuscle &QuadratusFemoris = ..SideHumanFolderRef.Leg.Mus.QuadratusFemoris;
   };
};

// The study: Operations to be performed on the model
AnyBodyCalibrationStudy LegCalibrationStudy2 = {
  AnyFolder &ref=.LegCal2; 
  nStep = 1;
};
