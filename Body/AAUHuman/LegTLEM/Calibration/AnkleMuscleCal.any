//This is the calibration1 file, it will calibrate all muscle in the leg in
//a neutral position (all joint angles in the drivers are zero)
//The subsequent calibrations 2 and 3 will overwrite some of the values
//obtained in this study

AnyFolder LegCalAnkle =
{  
  AnyFolder &Pelvis = ...HumanFolderRef.Trunk.SegmentsLumbar.PelvisSeg;
  AnyFolder &Sacrum = ...HumanFolderRef.Trunk.SegmentsLumbar.SacrumSeg;
  
  // Segments needed for the Psoas Major muscles
  AnyFolder& SegmentsLumbar = Main.HumanModel.BodyModel.Trunk.SegmentsLumbar;
  AnyFolder &Thorax = ...HumanFolderRef.Trunk.SegmentsThorax.ThoraxSeg;
  
  AnyFolder &LegSegSide = .SideHumanFolderRef.Leg.Seg;
  
  AnyFolder &LegJointsSide = .SideHumanFolderRef.Leg.Jnt;
  AnyFolder &TrunkJoints = ...HumanFolderRef.Trunk.JointsLumbar;
  
  AnyFolder &MusCalSide =  .SideHumanFolderRef.Leg.Mus;
  
  // Ankle position variables // PlantarFlexion - SubtalarEversion
  AnyFloat PlantarFlexion      = {  20,   0} * pi/180;
  AnyFloat SubtalarEversion    = {   0,  20} * pi/180;
  AnyFloat DorsiFlexion        = { -45,   0} * pi/180;
  AnyFloat SubtalarInversion   = {   0, -30} * pi/180;
  
  AnyFloat KneeFlexed      = { 155} * pi/180;
  AnyFloat KneeExtended    = {  -3} * pi/180;
  
  AnyFloat AnklePositions = flatten(repmat(2,{
      PlantarFlexion[0],
      SubtalarEversion[0],
      DorsiFlexion[0],
      SubtalarInversion[0]
  }'));
  AnyFloat SubtalarPositions = flatten(repmat(2,{
      PlantarFlexion[1],
      SubtalarEversion[1],
      DorsiFlexion[1],
      SubtalarInversion[1]
  }'));
  AnyFloat KneePositions = flatten({repmat(4,KneeFlexed),repmat(4,KneeExtended)});
  
  
  AnyFixedRefFrame ground = 
  {
    AnyRefNode node = 
    {
      ARel=..Pelvis.Axes0;
      sRel=..Pelvis.r0;
    };
  };

  AnyKinEqDriver ThoraxFix = 
  {
    AnyKinMeasure& PelvisThoraxExtension = ....HumanFolderRef.Interface.Trunk.PelvisThoraxExtension;
    AnyKinMeasure& PelvisThoraxLateralBending = ....HumanFolderRef.Interface.Trunk.PelvisThoraxLateralBending;
    AnyKinMeasure& PelvisThoraxRotation = ....HumanFolderRef.Interface.Trunk.PelvisThoraxRotation;
  };
  
  AnyKinEqDriver PelvisFix = 
  {
    AnyKinLinear Lin = 
    {
      AnyRefFrame &ground = ..ground.node;
      AnyRefFrame &Pelvis = .....HumanFolderRef.Trunk.SegmentsLumbar.PelvisSeg;
    };
    AnyKinRotational Rot = 
    {
      Type = RotAxesAngles;
      AnyRefFrame &ground = ..ground.node;
      AnyRefFrame &Pelvis = .....HumanFolderRef.Trunk.SegmentsLumbar.PelvisSeg;
    };
  };
  
  AnyKinEqInterPolDriver Hip = 
  {
    AnyKinRotational &Spherical = ..SideInterfaceFolderRef.HipFlexion.HipMeasure;
    Type = PiecewiseLinear;
    T =     {0.0, 1.0};
    Data = {{0,  0},
            {0,  0},
            {0,  0}}*pi/180;
    Reaction.Type = {Off, Off, Off};
  };
  
  AnyKinEqInterPolDriver Knee = 
  {
    AnyKinMeasure &Hinge = ..SideInterfaceFolderRef.KneeFlexion;
    Type = PiecewiseLinear;
    T = linspace(0,1,SizesOf(Data)[1]);
    Data = {.KneePositions}; // ? degrees knee flexion
    Reaction.Type = {Off};
  };
  
  AnyKinEqInterPolDriver Ankle = 
  {
    AnyKinMeasure &Hinge = ..SideInterfaceFolderRef.AnklePlantarFlexion;
    Type = PiecewiseLinear;
    T = linspace(0,1,SizesOf(Data)[1]);
    Data = {.AnklePositions};
    Reaction.Type = {Off};
  };
  
  AnyKinEqInterPolDriver SubTalar = 
  {
    AnyKinMeasure &Hinge = ..SideInterfaceFolderRef.SubTalarEversion;
    Type = PiecewiseLinear;
    T = linspace(0,1,SizesOf(Data)[1]);
    Data = {.SubtalarPositions};
    Reaction.Type = {Off};
  };
  
  AnyString MusPrefix = CompleteNameOf(&.SideHumanFolderRef.Leg.Mus);
  
  AnyObjectPtrArray TibialisAnteriorMuscles = ObjSearch(MusPrefix+".TibialisAnterior*", "AnyMuscle");
  AnyFloat  TibialisAnteriorRmin = repmat(NumElemOf(TibialisAnteriorMuscles), 0.5);
  AnyFloat  TibialisAnteriorRmax = repmat(NumElemOf(TibialisAnteriorMuscles), 1.2);
  
  AnyObjectPtrArray ExtensorDigitorumLongusMuscles = ObjSearch(MusPrefix+".ExtensorDigitorumLongus*", "AnyMuscle");
  AnyFloat  ExtensorDigitorumLongusRmin = repmat(NumElemOf(ExtensorDigitorumLongusMuscles), 0.5);
  AnyFloat  ExtensorDigitorumLongusRmax = repmat(NumElemOf(ExtensorDigitorumLongusMuscles), 1.2);
  
  AnyObjectPtrArray ExtensorHallucisLongusMuscles = ObjSearch(MusPrefix+".ExtensorHallucisLongus*", "AnyMuscle");
  AnyFloat  ExtensorHallucisLongusRmin = repmat(NumElemOf(ExtensorHallucisLongusMuscles), 0.5);
  AnyFloat  ExtensorHallucisLongusRmax = repmat(NumElemOf(ExtensorHallucisLongusMuscles), 1.2);

  AnyObjectPtrArray SoleusMuscles = ObjSearch(MusPrefix+".Soleus*", "AnyMuscle");
  AnyFloat  SoleusRmin = repmat(NumElemOf(SoleusMuscles), 0.5);
  AnyFloat  SoleusRmax = repmat(NumElemOf(SoleusMuscles), 1.2);
  
  AnyObjectPtrArray TibialisPosteriorMuscles = ObjSearch(MusPrefix+".TibialisPosterior*", "AnyMuscle");
  AnyFloat  TibialisPosteriorRmin = repmat(NumElemOf(TibialisPosteriorMuscles), 0.5);
  AnyFloat  TibialisPosteriorRmax = repmat(NumElemOf(TibialisPosteriorMuscles), 1.2);
  
  AnyObjectPtrArray FlexorDigitorumLongusMuscles = ObjSearch(MusPrefix+".FlexorDigitorumLongus*", "AnyMuscle");
  AnyFloat  FlexorDigitorumLongusRmin = repmat(NumElemOf(FlexorDigitorumLongusMuscles), 0.5);
  AnyFloat  FlexorDigitorumLongusRmax = repmat(NumElemOf(FlexorDigitorumLongusMuscles), 1.2);

  AnyObjectPtrArray FlexorHallucisLongusMuscles = ObjSearch(MusPrefix+".FlexorHallucisLongus*", "AnyMuscle");
  AnyFloat  FlexorHallucisLongusRmin = repmat(NumElemOf(FlexorHallucisLongusMuscles), 0.5);
  AnyFloat  FlexorHallucisLongusRmax = repmat(NumElemOf(FlexorHallucisLongusMuscles), 1.2);
  
  AnyObjectPtrArray PeroneusBrevisMuscles = ObjSearch(MusPrefix+".PeroneusBrevis*", "AnyMuscle");
  AnyFloat  PeroneusBrevisRmin = repmat(NumElemOf(PeroneusBrevisMuscles), 0.5);
  AnyFloat  PeroneusBrevisRmax = repmat(NumElemOf(PeroneusBrevisMuscles), 1.2);
  
  AnyObjectPtrArray PeroneusLongusMuscles = ObjSearch(MusPrefix+".PeroneusLongus*", "AnyMuscle");
  AnyFloat  PeroneusLongusRmin = repmat(NumElemOf(PeroneusLongusMuscles), 0.5);
  AnyFloat  PeroneusLongusRmax = repmat(NumElemOf(PeroneusLongusMuscles), 1.2);
  
  AnyObjectPtrArray PeroneusTertiusMuscles = ObjSearch(MusPrefix+".PeroneusTertius*", "AnyMuscle");
  AnyFloat  PeroneusTertiusRmin = repmat(NumElemOf(PeroneusTertiusMuscles), 0.5);
  AnyFloat  PeroneusTertiusRmax = repmat(NumElemOf(PeroneusTertiusMuscles), 1.2);
  
  // Two-joint muscles
  AnyObjectPtrArray GastrocnemiusMuscles = ObjSearch(MusPrefix+".Gastrocnemius*", "AnyMuscle");
  AnyFloat  GastrocnemiusRmin = repmat(NumElemOf(GastrocnemiusMuscles), 0.5);
  AnyFloat  GastrocnemiusRmax = repmat(NumElemOf(GastrocnemiusMuscles), 1.2);
  
  AnyObjectPtrArray PlantarisMuscles = ObjSearch(MusPrefix+".Plantaris*", "AnyMuscle");
  AnyFloat  PlantarisRmin = repmat(NumElemOf(PlantarisMuscles), 0.5);
  AnyFloat  PlantarisRmax = repmat(NumElemOf(PlantarisMuscles), 1.2);

  
};

// The study: Operations to be performed on the model
AnyBodyCalibrationStudy LegCalibrationStudyAnkle = 
{
  AnyFolder &ref=.LegCalAnkle; 
  nStep = 8; 
  MuscleArr = arrcat(arrcat(
      ref.TibialisAnteriorMuscles,
      ref.ExtensorDigitorumLongusMuscles,
      ref.ExtensorHallucisLongusMuscles,
      ref.SoleusMuscles,
      ref.TibialisPosteriorMuscles,
      ref.FlexorDigitorumLongusMuscles,
      ref.FlexorHallucisLongusMuscles,
      ref.PeroneusBrevisMuscles,
      ref.PeroneusLongusMuscles,
      ref.PeroneusTertiusMuscles),
      ref.GastrocnemiusMuscles,
      ref.PlantarisMuscles
      

  );
  RminMuscleFiber = arrcat(arrcat(
      ref.TibialisAnteriorRmin,
      ref.ExtensorDigitorumLongusRmin,
      ref.ExtensorHallucisLongusRmin,
      ref.SoleusRmin,
      ref.TibialisPosteriorRmin,
      ref.FlexorDigitorumLongusRmin,
      ref.FlexorHallucisLongusRmin,
      ref.PeroneusBrevisRmin,
      ref.PeroneusLongusRmin,
      ref.PeroneusTertiusRmin),
      ref.GastrocnemiusRmin,
      ref.PlantarisRmin

  );
  RmaxMuscleFiber = arrcat(arrcat(
      ref.TibialisAnteriorRmax,
      ref.ExtensorDigitorumLongusRmax,
      ref.ExtensorHallucisLongusRmax,
      ref.SoleusRmax,
      ref.TibialisPosteriorRmax,
      ref.FlexorDigitorumLongusRmax,
      ref.FlexorHallucisLongusRmax,
      ref.PeroneusBrevisRmax,
      ref.PeroneusLongusRmax,
      ref.PeroneusTertiusRmax),
      ref.GastrocnemiusRmax,
      ref.PlantarisRmax
  );               
 Kinematics.PosAnalysisOnlyOnOff = On;
  
};

