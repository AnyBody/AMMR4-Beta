//This file contains the interface to the leg it consist of joints and 
//nodes which can be used for defining contact with the sorroundings


AnyKinMeasureOrg AnklePlantarFlexion={
  
  AnyFolder &Shank = ..Seg.Shank.AnkleJoint;
  Shank ={
    AnyRefNode RotNode={
      ARel={{0,....Sign*-1,0},{....Sign*1,0,0},{0,0,1}};
    };
  };
  AnyFolder &Talus = ..Seg.Talus.AnkleJoint;
  Talus ={
    AnyRefNode RotNode={
      ARel={{....Sign*1,0,0},{0,....Sign*1,0},{0,0,1}};
    };
  };
  AnyKinRotational Ankle = {
    Type=RotAxesAngles;
    AnyRefNode &ShankNode = ...Seg.Shank.AnkleJoint;
    AnyRefNode &FootNode  = ...Seg.Talus.AnkleJoint;
    
  }; // End of Ankle
  MeasureOrganizer={0};
};

//AnyKinMeasureOrg AnkleEversion={
//  AnyKinRotational &Ankle = .AnklePlantarFlexion.Ankle;
//  MeasureOrganizer={1};
//};



AnyKinMeasureOrg SubTalarEversion={
  
  AnyFolder &Talus = ..Seg.Talus.SubTalarJoint;
  Talus ={
    AnyRefNode RotNode={
      ARel={{0,....Sign*-1,0},{....Sign*1,0,0},{0,0,1}};
    };
  };
  AnyFolder &Foot = ..Seg.Foot.SubTalarJoint;
  Foot ={
    AnyRefNode RotNode={
      ARel={{....Sign*1,0,0},{0,....Sign*1,0},{0,0,1}};
    };
  };
  AnyKinRotational Ankle = {
    Type=RotAxesAngles;
    AnyRefNode &TalusNode = ...Seg.Talus.SubTalarJoint;
    AnyRefNode &SubTalarNode  = ...Seg.Foot.SubTalarJoint;
   
  }; // End of SubTalar
  MeasureOrganizer={0};
};




AnyKinMeasureOrg KneeFlexion = {
  AnyFolder &Thigh = ..Seg.Thigh.KneeJoint;
  Thigh ={
    AnyRefNode RotNode={
      ARel={{....Sign*1,0,0},{0,....Sign*1,0},{0,0,1}};
    };
  };
  AnyFolder &Shank = ..Seg.Shank.KneeJoint;
  Shank ={
    AnyRefNode RotNode={
      ARel={{....Sign*1,0,0},{0,....Sign*1,0},{0,0,1}};
    };
  };
  AnyKinRotational KneeRot={
    AnyRefNode &ShankNode = ...Seg.Shank.KneeJoint.RotNode;
    AnyRefNode &ThighNode = ...Seg.Thigh.KneeJoint.RotNode;
    Type=RotAxesAngles;
  };
  MeasureOrganizer={0};
}; 
//
//
//
//AnyKinMeasureOrg PatellaFlexion = {
//  AnyFolder &Thigh = ..Seg.Thigh.PatellaFemurJoint;
//  Thigh ={
//    AnyRefNode RotNode={
//      ARel={{....Sign*1,0,0},{0,....Sign*1,0},{0,0,1}};
//    };
//  };
//  AnyFolder &Patella = ..Seg.Patella.PatellaFemurJoint;
//  Patella ={
//    AnyRefNode RotNode={
//      ARel={{....Sign*1,0,0},{0,....Sign*1,0},{0,0,1}};
//    };
//  };
//  AnyKinRotational PatellaMeasure={
//    AnyRefNode &ThighNode = ...Seg.Thigh.PatellaFemurJoint.RotNode;
//    AnyRefNode &PatellaNode = ...Seg.Patella.PatellaFemurJoint.RotNode;
//    Type=RotAxesAngles;
//  };
//  MeasureOrganizer={0};
//}; 
//
//AnyKinMeasureOrg PatellaRotation = {
//  AnyKinRotational &PatellaFemurJoint =.PatellaFlexion.PatellaMeasure; 
//  MeasureOrganizer={1};
//};
//

/// This Folder contains hip angle measures which are compliant the sequence of 
/// hip joint rotation sequence recommended by International Society of 
/// Biomechanics (ISB). The seqence is hip flexion (Z), hip abduction (X) and hip
/// external rotation (Y). These measuers in this folder are used by default in the
/// interface folder. 
AnyFolder HipJoint_SeqZXY = {
  AnyKinMeasureOrg HipFlexion = {
    AnyKinRotational HipMeasure ={
      AnyRefNode &PelvisNode = ....HipNodeRef.HipJoint.RotNode;
      AnyRefNode &ThighNode = ....Seg.Thigh.HipJoint.RotNode;
      PelvisNode={AnyDrawRefFrame drw={RGB={1,0,0};Visible = Off;}; };
      ThighNode={AnyDrawRefFrame drw={RGB={0,1,0};Visible = Off;};  };
      Axis1 = z; Axis2 = x;  Axis3 = y;
      Type=RotAxesAngles;
    };
    MeasureOrganizer={0};
  };
  AnyKinMeasureOrg HipAbduction = {
    AnyKinRotational &HipJoint =.HipFlexion.HipMeasure; 
    MeasureOrganizer={1};
  };
  AnyKinMeasureOrg HipExternalRotation ={
    AnyKinRotational &HipJoint =.HipFlexion.HipMeasure; 
    MeasureOrganizer={2};
  };
};

/// This Folder contains hip angle measures which use the rotation sequence
/// applied by AMMR 1.4 and earlier. The sequence of rotation is Hip flexion (Z), 
/// Hip external roation (Y) and Hip Abduction (X). This sequence is no longer the
/// the default in AMMR 1.5, since it is not compliant with the recommendations of
/// the International Society of Biomechanics (ISB). 
/// The measures are provided for backward compatability, and can be activated by 
/// setting the flag #define USE_AMMR14_HIP_ROTATIONS
AnyFolder HipJoint_SeqZYX = {

  AnyKinMeasureOrg HipFlexion = {
    //create new rotated node 
    AnyFolder &HipJointRef=...HipNodeRef.HipJoint;
    HipJointRef={
      AnyRefNode RotNode={
        ARel= {{.....Sign*-1,0,0},{0,.....Sign*-1,0},{0,0,1}};
      };
    };
    AnyFolder &ThighNodeRef = ...Seg.Thigh.HipJoint;
    ThighNodeRef ={
      AnyRefNode RotNode={
        ARel={{.....Sign*-1,0,0},{0,.....Sign*-1,0},{0,0,1}};
      };
    };
    AnyKinRotational HipMeasure ={
      AnyRefNode &PelvisNode = ....HipNodeRef.HipJoint.RotNode;
      AnyRefNode &ThighNode = ....Seg.Thigh.HipJoint.RotNode;
      
      PelvisNode={//AnyDrawRefFrame drw={RGB={1,0,0};};
      };
      ThighNode={//AnyDrawRefFrame drw={RGB={0,1,0};};
      };
  
      Type=RotAxesAngles;
    };
    MeasureOrganizer={0};
  };
  AnyKinMeasureOrg HipAbduction = {
    AnyKinRotational &HipJoint =.HipFlexion.HipMeasure; 
    MeasureOrganizer={2};
  };
  AnyKinMeasureOrg HipExternalRotation = {
    AnyKinRotational &HipJoint =.HipFlexion.HipMeasure; 
    MeasureOrganizer={1};
  };

};
#ifndef USE_AMMR14_HIP_ROTATIONS
AnyKinMeasureOrg &HipFlexion = HipJoint_SeqZXY.HipFlexion;
AnyKinMeasureOrg &HipAbduction = HipJoint_SeqZXY.HipAbduction;
AnyKinMeasureOrg &HipExternalRotation = HipJoint_SeqZXY.HipExternalRotation;
#else 
/// This message warns the user that the model is loaded with hip angles measured 
/// as it was done in AMMR < 1.5. The sequence of rotation is Hip flexion (Z), 
/// Hip external roation (Y) and Hip Abduction (X). This sequence is no longer the
/// the default in AMMR 1.5, since it is not compliant with the recommendations of
/// the International Society of Biomechanics (ISB). 
///
/// This notice appears since the flag #define USE_AMMR14_HIP_ROTATIONS have been set.
AnyMessage HipRotationSequenceMessage = {
  TriggerConst = On;
  Message = "Model is using the old hip rotation sequence (AMMR < 1.5).";
};
AnyKinMeasureOrg &HipFlexion = HipJoint_SeqZYX.HipFlexion;
AnyKinMeasureOrg &HipAbduction = HipJoint_SeqZYX.HipAbduction;
AnyKinMeasureOrg &HipExternalRotation = HipJoint_SeqZYX.HipExternalRotation;
#endif

AnyKinMeasureOrg HipFlexionJntMus = {
  AnyKinRotational HipMeasure ={
    AnyRefNode &PelvisNode = ...HipNodeRef.HipJoint.RotNode;
    AnyRefNode &ThighNode = ...Seg.Thigh.HipJoint.RotNode;
    AngVelOnOff = On; // xyz vector output
    Type=RotAxesAngles;
  };
  MeasureOrganizer={2};
};
AnyKinMeasureOrg HipAbductionJntMus = {
  AnyKinRotational &HipJoint =.HipFlexionJntMus.HipMeasure; 
  MeasureOrganizer={0};
};
AnyKinMeasureOrg HipExternalRotationJntMus ={
  AnyKinRotational &HipJoint =.HipFlexionJntMus.HipMeasure; 
  MeasureOrganizer={1};
};




AnyRefNode &HeelContactNode =.Seg.Foot.HeelNode;
AnyRefNode &ToeMedialContactNode  =.Seg.Foot.ToeMedialContactNode;
AnyRefNode &ToeLateralContactNode =.Seg.Foot.ToeLateralContactNode;




