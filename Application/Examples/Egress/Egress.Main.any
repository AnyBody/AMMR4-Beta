/** -----------------------------------------------------------------------------
This model deals with the problem of getting out of a car - so called Egress.
For many elderly people, the task of getting up from a low car seat in a
situation where limited space is available can be quite strenous.

This model simulates an egress movement from where the occupant has just left
the support of the seat and until he or she is standing up.

The support points are:
- The left foot on the road outside the car
- The right foot on the floor inside the car
- The right hand holding on to a handle on the window frame

The question the model must answer is:
- What is the best position of the handle along the window frame?

The model investigates the muscular effort for varying positions of 
the window frame handle as it is moved from a low position to
a high position.

Notice that the model is not driven by actual motion capture data. Most
of the kinematics of the model is determined by the contact to the support
points. Additionally the pelvis is driven along an interpolated spline 
curve.*/
Main = {
  
  /**Execute this operation to run the model in the intended operation sequence.  
  It is also possible to run operations seperately, by manual selections in the operation tree*/
  AnyOperationSequence RunApplication = {

    /**This operation calibrates the muscles in the model if these are of the type AnyMuscleModel3E.
    This will just be an empty operation if the model is using a muscle type that does not require calibration.*/
    AnyOperation &CalibrationAnal = Main.HumanModel.Calibration.CalibrationSequence;  

    ///This operation is the inverse dynamic analysis
    AnyOperation &InvAnal=Main.Study.InverseDynamics;
  };

  
  AnyFolder EnvironmentParameters = {
    
    //Position of the rigth heel
    AnyVar HeelXR =   0.8;
    AnyVar HeelYR =  -0.35;
    AnyVar HeelZR =  -0.15; 
    
    //Position if the left heel
    AnyVar HeelXL =   0.3;
    AnyVar HeelYL =  -0.68;
    AnyVar HeelZL =  -0.5; 
    
    //Knee position right
    AnyVar KneeXR = 0.5;
    AnyVar KneeYR = 0.0;
    AnyVar KneeZR = -0.2;
    
    //Knee position left
    AnyVar KneeXL = 0.5;
    AnyVar KneeYL = 0.0;
    AnyVar KneeZL = -0.5;
    
    //Elbow position right
    AnyVar ElbowXR = 0.3;
    AnyVar ElbowYR = 0.4;
    AnyVar ElbowZR = -0.05; 
    
    //Elbow position left
    AnyVar ElbowXL = 0.3;
    AnyVar ElbowYL = 0.4;
    AnyVar ElbowZL = -0.2;
    
    //posiotn of the handle
    AnyVar HandleX = 0.4;
    AnyVar HandleY = 0.6;
    AnyVar HandleZ = -0.3;   
    
    // Determines the position of the handle along the window frame
    // changing the value will move the handle up or down
    AnyVar HandlePos = -0.2;
  };
  
  // Specification of the graphical appearance of the model
  #include "DrawSettings.any"
  
  AnyFolder HumanModel = {
    AnyFolder &Mannequin=.Model.Mannequin;
    
    // Use only ONE of the three include fles below at any time
    // --------------------------------------------------------
    
    // Selecting the include file below results in a body model without muscles.
    // Instead, the model is equipped with joint moment providers. This version
    // loads and run much faster than versions with anatomical muscles.
    // #include  "..\..\..\Body\AAUHuman\BodyModels\FullBodyModel\BodyModel_NoMuscles.any"    
    
    // Selecting the include file below results in a body model with simple
    // constant stength muscles. They are adequate for many purposes and have the
    // advantage of not requiring calibration
    #include  "..\..\..\Body\AAUHuman\BodyModels\FullBodyModel\BodyModel.any"
    
    // The include file below selects the three-element Hill-type muscles.
    // They require calibration before they can be used, and this can be done
    // by the HumanModel.Calibration.CalibrationSequence operation. This must 
    // be executed every time the model is reloaded.
    //#include  "..\..\..\Body\AAUHuman\BodyModels\FullBodyModel\BodyModel_Mus3E.any" 
    
    // The StrengthParameters folder allows for simple scaling of strength
    // between the spine, the legs and the ShoulderArm body parts. The methods
    // are slightly different to adhere to the anatomical literature on which the
    // models are based.
    AnyFolder StrengthParameters={
      AnyVar SpecificMuscleTensionSpine= 90; //N/cm^2
      AnyVar StrengthIndexLeg= 1; 
      AnyVar SpecificMuscleTensionShoulderArm= 90; //N/cm^2
    };
    
    // This section below offers the choice between two scaling strategies for the
    // human model. 
    
    // ScalingStandard means that all the body parts will have the standard size 
    // at which they were originally developed, i.e. with anthropometric
    // data from the anatomical literature. This roughly corresponds to a 50th
    // percentile European male.    
    #include "..\..\..\Body\AAUHuman\Scaling\ScalingStandard.any"
    
    // ScalingLengthMassFat will scale each segment of the body to anthropometric data
    // specified in the selected AnyFamily include file, attempting to take the
    // fat percentage into account in the assessment of the muscle strength.
    // #include "..\..\..\Body\AAUHuman\Scaling\ScalingLengthMassFat.any" 
    // Scaling = {
    //   #include "..\..\..\Body\AAUHuman\Scaling\AnyFamily\AnyMan.any"
    // };
  };

  /// This folder collects all the model elements and is the target of the
  /// analyses performed by the study
  AnyFolder Model = {
    AnyFolder &HumanModel=.HumanModel.BodyModel;
    #include "Mannequin.any" 
    
    /// The environment, i.e. the car and ground.
    AnyFolder EnvironmentModel = {
      #include "Environment.any"
    };
    
    /// Elements connecting the body to the environment
    AnyFolder ModelEnvironmentConnection = {
      #include "JointsAndDrivers.any"
    };
  };
  
  AnyFolder HandlePosStudyModel1 = {
    AnyFolder &Model = .Model; 
    AnyFolder AdditionalJointsAndDriver = {
      #include "HandlePosStudyJointsAndDrivers1.any"
    };
  };
  
  ///In this study the handle for the right hand changes position along
  ///the window frame. The left arm is unsupported
  AnyBodyStudy Study = {
    AnyFolder &Model = .HandlePosStudyModel1; 
    tEnd = 10.0;
    Gravity = {0.0, -9.81, 0.0};
    nStep = 11;
  }; // End of study
  
}; //Main
