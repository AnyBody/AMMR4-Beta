/** This is a stand-alone instructional model of a jumping driven by
a pull string. The model shows how to create segments, joints and drivers
and how AnyScript as s declarative language frees allows the user to
simply declare the presence of objects whereafter the system sorts out
the connections and computes the dynamics. */
Main = {
   /**Execute this operation to run the model in the intended operation sequence.  
  It is also possible to run operations seperately, by manual selections in the operation tree*/
  AnyOperationSequence RunApplication = {

    ///This operation is the inverse dynamic analysis
    AnyOperation &InvAnal=Main.Study.InverseDynamics;
 
  };


  // The actual body model goes in this folder
  AnyFolder MyModel = {
    
    /// The Global Reference Frame, i.e. ground.
    AnyFixedRefFrame GlobalRef = {
      AnyRefNode HandleBase={
        sRel={0,-0.4,0};
      };
    };  // Global reference frame
    
    AnyFolder  Colors = {
      AnyVec3 AnyBodyRed = {149/256,51/256,55/256};    //AnyBody standard red
      AnyVec3 AnyBodyGreen = {47/256,131/256,80/256};  //AnyBody standard green
      AnyVec3 AnyBodyBlue = {82/256,85/256,111/256};   //AnyBody standard blue
      AnyVec3 AnyBodyYellow= {235/256,197/256,17/256}; //AnyBody standard yellow
      AnyVec3 AnyBodyPaleYellow = {248/256,204/256,115/256}; //AnyBody standard pale yellow
      AnyVec3 AnyBodyGrey = {153/256,153/256,153/256};    //AnyBody standard grey
      AnyVar Transparency = 1;
    };
    
    AnySeg Body = {
      Mass=0.00013025*750;
      Jii={6.038*10^-7,6.14*10^-8,6.61*10^-7}*750;
      sCoM={0,-0.0768,0};
      
      AnyDrawSTL drw1={
        FileName ="BodyBase.stl";
        RGB=..Colors.AnyBodyGreen;
        Transparency=..Colors.Transparency;
      };
      
      AnyDrawSTL drw2={
        FileName ="BodyDecor.stl";
        RGB=..Colors.AnyBodyRed;
        Transparency=..Colors.Transparency;
      };
      
      AnyRefNode NailHole ={
        sRel={0,0,0};
      };
      
      AnyRefNode RightArmJntNode={
        sRel={-0.043,-0.082,0};
      };
      
      AnyRefNode LeftArmJntNode={
        sRel={0.043,-0.082,0};
      };
      
      AnyRefNode RightLegJntNode={
        sRel={-0.037,-0.207,0};
      };
      
      AnyRefNode LeftLegJntNode={
        sRel={0.037,-0.207,0};
      };  
      
      AnyRefNode StringNodeTop={
        sRel={-0.0,-0.12,0};
      };
      
      AnyRefNode StringNodeBottom={
        sRel={-0.0,-0.207,0};
      };      
      
      AnyRefNode LogoLocation={
        sRel={-0.0,-0.12,0.0045};
      };
    };
    
    AnySeg RightArm = {
      Jii={7.7863*10^-10,2.1221*10^-8,2.196*10^-8}*750;
      Mass=1.403*10^-5*750; // The mass should always be defined
      r0 ={-0.097000, -0.083000, 0.000000};
      
      AnyDrawSTL drw={
        FileName ="arm.stl";
        RGB=..Colors.AnyBodyBlue;
        Transparency=..Colors.Transparency;
      };
      
      AnyRefNode JntNode={        sRel={0.054,0.001,0};      };
      AnyRefNode StringNode={        sRel={0.065,0.001,0};      };  
    };
    
    
    AnySeg LeftArm = {
      Jii={7.7863*10^-10,2.1221*10^-8,2.196*10^-8}*750;
      //Jij={-4.492*10^-11,0,0}*750;
      Mass=1.403*10^-5*750;
      Axes0 = {{-1.000000, -0.000000, 0.000000}, {0.000000, -1.000000, 0.000000}, {0.000000, 0.000000, 1.000000}};
      r0 ={0.097000, -0.083000, 0.000000};
      
      AnyDrawSTL drw={
        FileName ="arm.stl";
        RGB=..Colors.AnyBodyBlue;
        ScaleXYZ={1,-1,1};
        Transparency=..Colors.Transparency;
      };
      
      AnyRefNode JntNode={
        sRel={0.054,-0.001,0};
      };
      
      AnyRefNode StringNode={
        sRel={0.065,-0.001,0};
      };
    };
    
    AnySeg RightLeg = {
      Mass=2.5*10^-5*750;  //volume(from CAD program) * density (guestimate for wood)
      Jii={7.64*10^-8,4.605*10^-9,8.0946*10^-8}*750;
      //Jij={-1.02*10^-9,0,0}*750;
      r0 ={-0.046000, -0.283000, 0.000000};
      
      AnyDrawSTL drw={
        FileName ="leg.stl";
        RGB=..Colors.AnyBodyBlue;
        Transparency=..Colors.Transparency;
      };
      
      AnyRefNode StringNode={
        sRel={0.013,0.089,0};
      };
      
      AnyRefNode JntNode={
        sRel={0.009,0.076,0};
      };      
    };
    
    AnySeg LeftLeg = {
      Mass=2.5*10^-5*750;  //volume(from CAD program) * density (guestimate for wood)
      Jii={7.64*10^-8,4.605*10^-9,8.0946*10^-8}*750;
      //Jij={-1.02*10^-9,0,0}*750;
      r0 ={0.046000, -0.283000, 0.000000};
      AnyDrawSTL drw={
        FileName ="leg.stl";
        RGB=..Colors.AnyBodyBlue;
        ScaleXYZ={-1,1,1};
        Transparency=..Colors.Transparency;
      };
      
      AnyRefNode StringNode={
        sRel={-0.013,0.089,0};
      };
      
      AnyRefNode JntNode={
        sRel={-0.009,0.076,0};
      };
    };
    
    AnySeg Handle ={
      Mass=9.869*10^-6*750;
      Jii={2.13*10^-9,2.13*10^-9,4.147*10^-9}*750;
      r0 ={0.000000, -0.399000, 0.000000};
      AnyDrawSurf drw={
        FileName ="handle";
        RGB=..Colors.AnyBodyGreen;
      };
    };
    
    AnySeg Logo={
      Mass=0;
      Jii={0,0,0};
      
      AnyDrawSTL Body = {
        FileName = "../../../Body/AAUHuman/ToolBox/DrawObjects/Body-grey.stl";
        RGB = ..Colors.AnyBodyRed;
        
        ScaleXYZ={0.002,0.002,0.002};
        Transparency=..Colors.Transparency;
      };
      
      AnyDrawSTL AnyTechnology = {
        FileName = "../../../Body/AAUHuman/ToolBox/DrawObjects/AnyTechnology-red.stl";
        RGB = ..Colors.AnyBodyGrey;
        
        ScaleXYZ={0.002,0.002,0.002};
        Transparency=..Colors.Transparency;
      };
    };
    
    AnySphericalJoint LogoJnt={
      AnyRefNode &ref1=.Body.LogoLocation;
      AnySeg &ref2=.Logo;
    };
    
    AnyKinEqSimpleDriver LogoJntDrv={
      AnySphericalJoint &ref1=.LogoJnt;
      DriverPos={0,0,0};
      DriverVel={0,0.0,0};
    };
    
    AnyRevoluteJoint NailBodyJnt={
      AnyRefFrame &ref1=.GlobalRef;
      AnyRefFrame &ref2=.Body.NailHole;
    }; 
    
    AnyKinEqSimpleDriver NailBodyDrv={
      AnyRevoluteJoint &ref=.NailBodyJnt;
      DriverPos={0};
      DriverVel={0};
    };
    
    AnyRevoluteJoint RightArmJnt={
      AnyRefFrame &ref1=.Body.RightArmJntNode;
      AnyRefFrame &ref2=.RightArm.JntNode;
    };
    
    AnyRevoluteJoint LeftArmJnt={
      AnyRefFrame &ref1=.Body.LeftArmJntNode;
      AnyRefFrame &ref2=.LeftArm.JntNode;
    }; 
    
    AnyRevoluteJoint RightLegJnt={
      AnyRefFrame &ref1=.Body.RightLegJntNode;
      AnyRefFrame &ref2=.RightLeg.JntNode;
    }; 
    
    AnyRevoluteJoint LeftLegJnt={
      AnyRefFrame &ref1=.Body.LeftLegJntNode;
      AnyRefFrame &ref2=.LeftLeg.JntNode;
    }; 
    
    AnyPrismaticJoint HandleJnt={
      AnyRefFrame &ref1=.GlobalRef.HandleBase;
      AnyRefFrame &ref2=.Handle;
      Axis=y;
    };
    
    /// Fourier drivers are useful for specifying
    /// cyclic or repetetive movements
    AnyKinEqFourierDriver HandleJntDrv={
      AnyPrismaticJoint &ref=.HandleJnt;
      
      Type = Cos;
      AnyVar a0 = 0.00125;
      AnyVar a1 = 0.008;
      AnyVar b0 = 0;
      AnyVar b1 = 0;
      
      Freq = 1*0.1; 
      A = { {a0,a1}  };
      B = {{0,b1} }; 
    };
    
    AnyKinPLine RightArmString ={
      AnyRefNode &ref1=.RightArm.StringNode;
      AnyRefNode &ref2=.Body.StringNodeTop;
      AnySeg &ref3=.Handle;   
      AnyDrawPLine drw={Thickness=0.0015; RGB={1,0,0};};
    };
    
    AnyKinPLine LeftArmString ={
      AnyRefNode &ref1=.LeftArm.StringNode;
      AnyRefNode &ref2=.Body.StringNodeTop;
      AnySeg &ref3=.Handle;   
      AnyDrawPLine drw={Thickness=0.0015; RGB={1,0,0};};
    };    
    
    AnyKinPLine RightLegString ={
      AnyRefNode &ref1=.RightLeg.StringNode;
      AnyRefNode &ref2=.Body.StringNodeBottom;
      AnySeg &ref3=.Handle;   
      AnyDrawPLine drw={Thickness=0.0015; RGB={1,0,0};};
    };
    
    AnyKinPLine LeftLegString ={
      AnyRefNode &ref1=.LeftLeg.StringNode;
      AnyRefNode &ref2=.Body.StringNodeBottom;
      AnySeg &ref3=.Handle;   
      AnyDrawPLine drw={Thickness=0.0015; RGB={1,0,0};};
    }; 
    
    AnyKinEqSimpleDriver ArmStringsDrv={
      AnyKinPLine &ref1=.RightArmString;
      AnyKinPLine &ref2=.LeftArmString;
      DriverPos={1,1}*0.334689;
      DriverVel={0.00,0.00};   
    };
    
    AnyKinEqSimpleDriver LegStringsDrv={
      AnyKinPLine &ref1=.RightLegString;  
      AnyKinPLine &ref2=.LeftLegString;
      DriverPos={1,1}*0.223478;
      DriverVel={0.0,0.0}; 
    };
  }; // MyModel
  
  // The study: Operations to be performed on the model
  AnyBodyStudy Study = {
    AnyFolder &Model = .MyModel;
    
    Gravity = {0.0, -9.81, 0.0};
    tEnd=10;
    nStep=40;
  };

  
  
};  // Main

