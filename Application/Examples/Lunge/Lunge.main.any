/** Forward Lunge model.

   Forward lunge is a motion used for testing the function of ACL patients.
   There is experimental evidence that ACL deficient patients divide into 
   two distinct groups: Copers and non-copers. The former distinguish 
   themselves by being able to perform a forward lunge movement as well 
   as individuals that have an intact ACL, whereas the latter take more
   time to do the movement.
   
   The motion data for this example is kindly provided by Tine Alkjaer
   of the University of Copenhagen. The motion data is sbsequently processed

   by Michael Skipper Andersen from Aalborg university to obain an optimized 
   movement in the sense that the model follows the marker trajectories as well
   as possible. */
Main = {
  
   /**Execute this operation to run the model in the intended operation sequence.  
  It is also possible to run operations seperately, by manual selections in the operation tree*/
  AnyOperationSequence RunApplication = {

    /**This operation calibrates the muscles in the model if these are of the type AnyMuscleModel3E.
    This will just be an empty operation if the model is using a muscle type that does not require calibration.*/
    AnyOperation &CalibrationAnal = Main.HumanModel.Calibration.CalibrationSequence;  

    ///This operation is the inverse dynamic analysis
    AnyOperation &InvAnal=Main.Study.InverseDynamics;
   
  };


  //Includes some settings for the visual representation of the model
  #include "DrawSettings.any"
  
  AnyFolder HumanModel = {
    
    AnyFolder &Mannequin=.Model.Mannequin;
    
    // The include file below selects the three-element Hill-type muscles.
    // They require calibration before they can be used, and this can be done
    // by the HumanModel.Calibration.CalibrationSequence operation. This must 
    // be executed every time the model is reloaded.
    #include  "..\..\..\Body\AAUHuman\BodyModels\TrunkNoMusclesTwoLegs\BodyModel_Mus3E.any"
    
    AnyFolder StrengthParameters={
      AnyVar SpecificMuscleTensionSpine= 150; //N/cm^2
      AnyVar StrengthIndexLeg= 1.5; 
      AnyVar SpecificMuscleTensionShoulderArm= 150; //N/cm^2
    };
    
    // Scale wrt. segments lengths, segmant mass and fat percentage
    #include "..\..\..\Body\AAUHuman\Scaling\ScalingLengthMassFat.any" 
    
    Scaling = {
      //This is the file containing the optimized segments lenghts
      #include "output-par.any"
      #include "AnyMan.any" 
    };
    
    //include the marker set
    #include "MarkerPlacement.any"
  };
  
  AnyFolder Model = {
    
    AnyFolder &HumanModel=.HumanModel.BodyModel;    
    
    //Create segments for each of the markers and drive them according to data
    //which can be found in the files p1.any - p15.any
    #include "MotionTrackerInclude.any"    
    #include "Mannequin.any" 
    
    // Environment around the human
    //Model of the floor and force plates 
    #include "Environment.any"
    
    //Connection between environment and the human    
    AnyFolder ModelEnvironmentConnection = {
      //Drivers for the model
      #include "JointsAndDrivers.any"
    };
  };
  
  // Simulate the missing cruciate ligaments by removing a reaction force in 
  // the x-direction measured in the shank coordinate system. Notice that
  // this eliminates the function of the PCL as well as the ACL.  
  Main.Model.HumanModel.Right.Leg.Jnt.Knee = {
    Ref = 1;  // Use the tibia coordinate system
    
    // Set the first component to Off to remove the cruciate ligaments.
    Constraints.Reaction.Type = {On,On,On,On,On};
  };
  
  // The study: Operations to be performed on the model
  AnyBodyStudy Study = {
    AnyFolder &Model = .Model;
    
    nStep=41;
    Gravity = {0.0, 0.0, -9.81};
    tEnd=8.0e-001;
    tStart=2.0487805e-002;
  };
  
};  // Main
